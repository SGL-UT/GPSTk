# This is a CTest script for a Linux continuous build.  It is meant to be called like so:
# ctest -S <path-to-script> -V
# However this is the "continuous" version which is intended to be
# launched as a scheduled task.  See the README in this directory.
#
# Project and configuration-specific parameters are defined in the block of
# set commands that follow.  We also rely on CTestConfig.cmake in the root
# of our source directory.
#
# Do note that the initial checkout of the source must be done manually
# before ever calling this script.  e.g.,
# git clone gitlab@repositories.arlut.utexas.edu:sgl/gpstk.git

set(CTEST_SOURCE_DIRECTORY "$ENV{HOME}/git/cdash/continuous/Linux/gpstk")
set(CTEST_BINARY_DIRECTORY "$ENV{HOME}/git/cdash/continuous/Linux/gpstk/build")
set(CTEST_SITE "clau.arlut.utexas.edu")
set(CTEST_BUILD_NAME "linux64-continuous")
set(CTEST_CMAKE_GENERATOR "Unix Makefiles")
set(CTEST_CONFIGURATION_TYPE "Release")
set(REPO "gitlab@repositories.arlut.utexas.edu:sgl/gpstk.git")
set(DEBUG_VALUE "false")

find_program(CTEST_GIT_COMMAND NAMES git)
find_program(CTEST_CMAKE_COMMAND NAMES cmake)

set(CTEST_CMAKE_COMMAND "${CTEST_CMAKE_COMMAND} -Wno-dev -DDEBUG_SWITCH=${DEBUG_VALUE} -DTEST_SWITCH=ON -DBUILD_EXT=ON")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CMAKE_COMMAND} \"-G${CTEST_CMAKE_GENERATOR}\"")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} ${CTEST_SOURCE_DIRECTORY}")

# Here we go...
while (${CTEST_ELAPSED_TIME} LESS 43200) # 7am-7pm
  set (START_TIME ${CTEST_ELAPSED_TIME})
  ctest_start ("Continuous")
  ctest_empty_binary_directory(${CTEST_BINARY_DIRECTORY})
  ctest_sleep(3) # to make sure bin directory has time to purge
  ctest_update (RETURN_VALUE count)
  if (count GREATER 0)
    ctest_configure()
    ctest_build()
    ctest_test()
    ctest_submit()
  endif ()
  ctest_sleep( ${START_TIME} 300 ${CTEST_ELAPSED_TIME})
endwhile()