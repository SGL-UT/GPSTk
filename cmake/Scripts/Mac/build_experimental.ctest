# This is a CTest script for a Mac OSX experimental build.  It is meant to be called like so:
# ctest -S scripts/build_experimental.ctest -V
#
# Project and configuration-specific parameters are defined in the block of
# set commands that follow.  We also rely on CTestConfig.cmake in the root
# of our source directory.
#
# Do note that the initial checkout of the source must be done manually
# before ever calling this script.  e.g.,
# git clone gitlab@repositories.arlut.utexas.edu:sgl/gpstk.git

set(CTEST_SOURCE_DIRECTORY "$ENV{HOME}/git/cdash/experimental/gpstk")
set(CTEST_BINARY_DIRECTORY "$ENV{HOME}/git/cdash/experimental/gpstk/build")
set(CTEST_SITE "applepie.arlut.utexas.edu")
set(CTEST_BUILD_NAME "macosx64-experimental")
set(CTEST_CMAKE_GENERATOR "Unix Makefiles")
set(CTEST_CONFIGURATION_TYPE "Release")
set(REPO "gitlab@repositories.arlut.utexas.edu:sgl/gpstk.git")
set(DEBUG_VALUE "false")

find_program(CTEST_GIT_COMMAND NAMES git)
find_program(CTEST_CMAKE_COMMAND NAMES cmake)

set(CTEST_CMAKE_COMMAND "${CTEST_CMAKE_COMMAND} -Wno-dev -DDEBUG_SWITCH=${DEBUG_VALUE} -DTEST_SWITCH=ON -DBUILD_EXT=ON")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CMAKE_COMMAND} \"-G${CTEST_CMAKE_GENERATOR}\"")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} ${CTEST_SOURCE_DIRECTORY}")

# Here we go...
ctest_start("Experimental")
execute_process(COMMAND date +%H:%M:%S
                OUTPUT_VARIABLE TIMESTAMP)
file(APPEND experimental_timing.log "CTest Startup:    " ${TIMESTAMP})
ctest_empty_binary_directory(${CTEST_BINARY_DIRECTORY})
ctest_sleep(3) # to make sure bin directory has time to purge
#ctest_update() # we do not update for an experimental build
ctest_configure()
ctest_build()
execute_process(COMMAND date +%H:%M:%S
                OUTPUT_VARIABLE TIMESTAMP)
file(APPEND experimental_timing.log "Tests Initiating: " ${TIMESTAMP})
ctest_test()
ctest_submit()